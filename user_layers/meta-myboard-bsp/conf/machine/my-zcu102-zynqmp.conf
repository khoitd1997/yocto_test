#@TYPE: Machine
#@NAME: zcu102-zynqmp
#@DESCRIPTION: Machine support for ZCU102 Evaluation Board.

require conf/machine/include/soc-zynqmp.inc
require conf/machine/include/machine-xilinx-default.inc
require conf/machine/include/machine-xilinx-qemu.inc

MACHINE_FEATURES = "rtc ext2 ext3 vfat usbhost"

UBOOT_MACHINE ?= "xilinx_zynqmp_virt_defconfig"
# SPL cannot be used by Zynqmp without some patches:
# https://xilinx-wiki.atlassian.net/wiki/spaces/A/pages/18842574/U-Boot+Secondary+Program+Loader
SPL_BINARY ?= ""

# Default SD image build onfiguration, use qemu-sd to pad
IMAGE_CLASSES += "image-types-xilinx-qemu"
IMAGE_FSTYPES += "wic.qemu-sd"
WKS_FILES ?= "sdboot.wks"

SERIAL_CONSOLES ?= "115200;ttyPS0"

KERNEL_DEVICETREE = "xilinx/zynqmp-zcu102-rev1.0.dtb"

EXTRA_IMAGEDEPENDS += " \
		arm-trusted-firmware \
		qemu-devicetrees \
		virtual/boot-bin \
		virtual/bootloader \
        u-boot-zynq-scr \
		"

# support values supported by the "compression" field in kernel part of its file
FITIMAGE_KERNEL_COMPRESSION ?= "none"

# taken from https://xilinx-wiki.atlassian.net/wiki/spaces/A/pages/18842374/U-Boot+Images
UBOOT_LOADADDRESS = "0x80000"
UBOOT_ENTRYPOINT = "0x80000"

IMAGE_BOOT_FILES += " \
		boot.scr \
        fitImage \
		"

SOC_VARIANT = "cg"

YAML_SERIAL_CONSOLE_STDOUT = "psu_uart_0"
YAML_DT_BOARD_FLAGS = "{BOARD zcu102-reva}"

# This machine has a QEMU model, runqemu setup:
QB_MEM = "-m 4096"
QB_OPT_APPEND ?= "-nographic -serial mon:stdio -serial null"
QB_NETWORK_DEVICE = "-net nic -net nic -net nic -net nic,netdev=net0,macaddr=@MAC@"

# Replicate BootROM like behaviour, having loaded SPL and PMU(ROM+FW)
QB_OPT_APPEND_append_qemuboot-xilinx = " \
		-hw-dtb ${DEPLOY_DIR_IMAGE}/qemu-hw-devicetrees/multiarch/zcu102-arm.dtb \
		${@qemu_zynqmp_unhalt(d, True)} \
		-device loader,file=${DEPLOY_DIR_IMAGE}/arm-trusted-firmware.elf,cpu-num=0 \
		-device loader,file=${DEPLOY_DIR_IMAGE}/u-boot.elf \
		-device loader,file=${DEPLOY_DIR_IMAGE}/system.dtb,addr=0x100000 \
		"

# Attach the rootfs disk image to the second SD interface of QEMU (which is SD0)
QB_DEFAULT_FSTYPE_qemuboot-xilinx = "wic.qemu-sd"
QB_OPT_APPEND_append_qemuboot-xilinx = " -boot mode=5"
QB_ROOTFS_OPT_qemuboot-xilinx = " -drive if=sd,index=1,file=@ROOTFS@,format=raw"

QB_PMU_OPT = " \
		-M microblaze-fdt \
		-display none \
		-hw-dtb ${DEPLOY_DIR_IMAGE}/qemu-hw-devicetrees/multiarch/zynqmp-pmu.dtb \
		-kernel ${PMU_ROM} \
		-device loader,file=${PMU_FIRMWARE_DEPLOY_DIR}/${PMU_FIRMWARE_IMAGE_NAME}.elf \
		-device loader,addr=0xfd1a0074,data=0x1011003,data-len=4 \
		-device loader,addr=0xfd1a007C,data=0x1010f03,data-len=4 \
		"
QB_OPT_APPEND_append_qemuboot-xilinx = " -pmu-args '${QB_PMU_OPT}'"

# by default, qemuboot.conf which controls which image is used for booting
# doesn't seem to get rebuilt when things like u-boot script get rebuilt
# so specify them here
do_write_qemuboot_conf[depends] += "u-boot-zynq-scr:do_deploy"

PREFERRED_PROVIDER_virtual/boot-bin = "xilinx-bootbin"